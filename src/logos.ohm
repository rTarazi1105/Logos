Logos {
	Program = Section+
	Section
		= DataDeclLine
		| StructDecl
		| EnumDecl
		| ClassDecl
		| MethodDecl
		| ModDecl
		| ClassImpl
	comment = "//" notNewLine*
	notNewLine = ~("\n" | "\r") any
	space += comment
		
		
	Statement
		= bool
		| Equality
		| FilledInfix
		| FilledOperation
		| FilledRelationOrProp
		| Negation
		| DataVar
		| ParenStatement
	ParenStatement = "(" Statement ")"
	Negation = "not" Statement
	
	FilledOperation = DataVar "[" ListOf<Statement, ","> "]"
	
	FilledRelationOrProp = DataVar "[" ListOf<DataVar, ","> "]"
	
	FilledInfix = Statement Infix Statement				// only works on binary operations
	
	Equality = DataVar "=" DataVar
	
	Infix
		= defaultInfix
		| UserDefinedInfix
	defaultInfix
		= "and"							// and
		| "or"							// or
		| "=="							// equals for statements
	
	DataDeclLine = DataDecl ";"
	DataDecl
		= ValueDecl
		| RelationDecl
		| OperationDecl
		| InfixDecl
		| PropertyDecl
		| StatementDecl
		| Assume
		
	ValueDecl = "value" id1 (":" DataVar)?
	RelationDecl = id1 ":" ListOf<id1, ","> ":" Statement
	OperationDecl = operationWord id2 ":" ListOf<id1, ","> ":" Statement
	InfixDecl = "infix" id2 Operation
	PropertyDecl = propertyWord id1 ":" ListOf<ArgRelation, ","> ":" Statement
	ArgRelation = id1 Numbering
	StatementDecl = id1 ":" Statement
	Assume = "assume" id1? bool ":" Statement
	
	Numbering = "<" number ">"
	
	StructDecl = "struct" id3 TypeParam? "{" ListOf<Param, ","> "}" 
	EnumDecl = "enum" id3 TypeParam? "{" ListOf<Param, ","> "}"
	
	ModDecl = "mod" id2 ModHead ModBody
	ModHead = TypeParam? "(" ListOf<ModParamWithSelf, ","> ")" ReturnType?
	// OptModParams = ListOf<ModParam, ",">
	
	MethodDecl = "mod" FilledCustomType "." id2 ModHead ModBody
	
	ModParamWithSelf = ModParam | SelfParam
	
	ReturnType = "->" mut? ValidType
	
	ClassDecl = "class" id3 TypeParam? SuperClass? ClassBody
	SuperClass = ":" ListOf<FilledCustomType, "+">
	ClassBody = "{" ClassMod* "}"
	ClassMod = "mod" id2 ModHead ModBodyOrColon
	ModBodyOrColon = ModBody | ";"
	
	ClassImpl = FilledCustomType "impl" FilledCustomType ClassBody
	
	TypeParam = "<" ListOf<IdAndSuperClass, ","> ">"		// no superclass = Any
	IdAndSuperClass = id3 SuperClass?
	TypeArgs = "<" ListOf<ValidType, ","> ">"	// FilledTypeParam
	
	Param = (id2 ":")? ValidType				// if no id: let it be 0,1 etc
	SelfParam = mut? "self"
	ModParam = id2 ":" mut? ValidType
	
	ValidType
		= defaultValidType
		| NumberedRelation
		| ArrayType
		| FilledCollection
		| FilledCustomType
	FilledCollection = collectionClass TypeArgs?
	FilledCustomType = CustomType TypeArgs?
	
	defaultValidType
		= "value"
		| "statement"
		| operationWord
		| propertyWord
		| "bool"
		| "int"
		| selfType
		| noneType
		| errorClass
		| comparableClass
		| equatableClass
	operationWord = "operation" | "op"
	propertyWord = "property" | "prop"
	
	NumberedRelation = "relation" Numbering?
	ArrayType = "[" ValidType ";" number? "]"
	
	ModBody = "{" ActionLine* "}"
	ActionLine = Action ";"
	Action
		= ModBody
		| DataDecl
		| Assignment
		| Increment | Decrement
		| Return
		| ControlFlow
		| BreakLine
		| ContinueLine
		| MethodCall
		| ModCall
	MethodCall = VarOrType "." id2 Call
	VarOrType = Variable | FilledCustomType
	ModCall = id2 Call
	Call = "(" ListOf<Readable, ","> ")"
	
	Assignment = Variable "=" Readable
	Readable = Construct | Literal | ControlFlow | LogosArray | Variable
	Construct = FilledCustomType "{" ListOf<Readable, ","> "}"
	Increment = Variable "++"
	Decrement = Variable "--"
	
	BreakLine = break+
	ContinueLine = continue+
	
	Return = returnOrYield Readable?
	returnOrYield = "return" | "yield"
	
	EvalBool
		= And
		| Or
		| Not
		| bool
		| InEqualities
		| Variable		// will need to add statement.truth
	And = EvalBool1 "&" EvalBool2
	Or = EvalBool1 "|" EvalBool2
	Not = "!" EvalBool
	EvalBool1 = EvalBool
	EvalBool2 = EvalBool
		
	InEqualities = Readable InEqual Readable
	InEqual = "<" | ">" | "==" | "!="
	
	ControlFlow = IfFlow | MatchFlow | ForFlow | WhileFlow
	IfFlow = if EvalBool then Action ElseFlow?
	ElseFlow = else Action
	ForFlow = for Arg in Collection do Action
	Arg = id2
	Collection = LogosArray | Variable
	WhileFlow = while EvalBool do Action
	MatchFlow = match Variable ":" ListOf<MatchLine, ",">
	MatchLine = MatchStart "=>" Action
	MatchStart
		= ValidType
		| IfEvalBool
	IfEvalBool = if EvalBool
	
	LogosArray						// range will be built-in. Count(int), Range(int, int).
		= FullArray
		| CopiedArray
	FullArray = "[" ListOf<Readable, ","> "]"
	CopiedArray = "[" Readable ";" Int "]"
	Int
		= number
		| Variable
	
	Variable
		= MethodCall
		| VarField
		| ModCall
		| AssignedVariable
	VarField = Variable "." Field				// field, list index, enum case
	
	Literal
		= bool
		| number
		| none
		| string
	string = "\"" char* "\""
	char = ~"\"" any
	bool = true | false
	
	number = digit+
	id1 = ~keyword idchar+					// data in header can start with a number
	id2 = ~keyword idletter idchar*				// variables, modules etc; must be lowercase
	id3 = ~keyword upper idchar*				// types must be capitalized
	idchar = alnum | "_"
	idletter = ~upper letter | "_"
	keyword = key " " // ~alnum
	key
		= bool
		| mut
		| defaultValidType
		| defaultInfix
		| returnOrYield
		| none
		| "infix"
		| "struct"
		| "enum"
		| "method"
		| "mod"
		| "class"
		| "impl"
		| if
		| then
		| else
		| for
		| in
		| do
		| while
		| "not"
		| break
		| continue
	//	| "len"
	
	
	DataVar = Variable | id1	// Can be a variable only if in mod
	Operation = Variable
	UserDefinedInfix = id2
	
	CustomType = id3			// struct, enum or class
	AssignedVariable = id2
	Field = id2 | number
	
	mut = "mut"
	none = "none"
	true = "true"
	false = "false"
	match = "match"
	if = "if"
	then = "then"
	else = "else"
	while = "while"
	for = "for"
	in = "in"
	do = "do"
	break = "break"
	continue = "continue"
	selfType = "Self"
	noneType = "None"
	errorClass = "Error"
	comparableClass = "Comparable"
	equatableClass = "Equatable"
	collectionClass = "Collection"
}
