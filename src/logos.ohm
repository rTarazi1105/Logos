Logos {
	Program = Section+
	Section
		= DataDeclLine
		| StructDecl
		| EnumDecl
		| ClassDecl
		| MethodDecl
		| ModDecl
		| ClassImpl
	// Comment = "//" notNewLine*
	// notNewLine = ~("\n" | "\r") any
		
		
	Statement
		= Bool
		| Negation
		| Equality
		| FilledInfix
		| FilledRelation
		| FilledOperation
		| FilledProperty
		| DeclaredStatement
		| ParenStatement
	ParenStatement = "(" Statement ")"
	Negation = "not" Statement
	FilledRelation = Relation "[" ListOf<Value, ","> "]"
	FilledOperation = Operation "[" ListOf<Statement, ","> "]"
	FilledInfix = Statement1 Infix Statement2				// only works on binary operations
	FilledProperty = Property "[" ListOf<Relation, ","> "]"
	Equality = Value1 "=" Value2
		
	Statement1 = Statement
	Statement2 = Statement
	Value1 = Value
	Value2 = Value
	
	Infix
		= "&&"							// and
		| "||"							// or
		| "=="							// equals for statements
		| UserDefinedInfix
	
	DataDeclLine = DataDecl ";"
	DataDecl
		= ValueDecl
		| RelationDecl
		| OperationDecl
		| InfixDecl
		| PropertyDecl
		| StatementDecl
		| Assume
		
	ValueDecl = "value" id1 (":" Relation)?
	RelationDecl = id1 ":" ListOf<ArgValue, ","> ":" Statement
	ArgValue = id1
	OperationDecl = "operation" id2 ":" ListOf<ArgStatement, ","> ":" Statement
	ArgStatement = id1
	InfixDecl = "infix" id2 Operation
	PropertyDecl = "property" id1 ":" ListOf<ArgRelation, ","> ":" Statement
	ArgRelation = id1 Numbering
	StatementDecl = id1 ":" Statement
	Assume = "assume" id1? Bool ":" Statement
	
	Numbering = "<" number ">"
	
	StructDecl = "struct" id2 TypeParam? SuperClass? "{" ListOf<Param, ","> "}" 
	EnumDecl = "enum" id2 TypeParam? "{" ListOf<Param, ","> "}"
	
	ModDecl = "mod" id2 ModHead ModBody
	ModHead = TypeParam? "(" ListOf<ModParam, ","> ")" ReturnType?
	// OptModParams = ListOf<ModParam, ",">
	
	MethodDecl = "mod" (Struct | Class) "." id2 MethodHead ModBody
	MethodHead = TypeParam? "(" ListOf<ModParamWithSelf, ",">? ")" ReturnType?
	ModParamWithSelf = ModParam | SelfParam
	
	ReturnType = "->" MutableValidTypeOrNone
	MutableValidTypeOrNone = MutableValidType | None
	MutableValidType = Mutable? ValidType
	None = "none"
	
	ClassDecl = "class" id2 TypeParam? SuperClass? ClassBody
	SuperClass = ":" ListOf<ClassInclError, ",">
	ClassInclError = Class | "Error"
	ClassBody = "{" ClassLine* "}"
	ClassLine
		= ClassField
		| ClassMod
	ClassField = Param ","
	ClassMod = "mod" id2 MethodHead ModBodyOrColon
	ModBodyOrColon = ModBody | ";"
	ClassImpl = ValidType "impl" Class ClassBody
	
	TypeParam = ("<" ListOf<IdAndSuperClass, ","> ">")		// no superclass = Any
	IdAndSuperClass = id2 SuperClass?
	FilledTypeParam = ("<" ListOf<ValidType, ","> ">")
	
	Param = (id2 ":")? ValidType				// if no id: let it be 0,1 etc
	SelfParam = Mutable? "self"
	ModParam = Mutable? Param
	Mutable = "mut"
	
	ValidType
		= DefaultValidType
		| NumberedRelation
		| ClassMaybeWithTypeParam
		| Struct
		| Enum
		| CollectionType
	ClassMaybeWithTypeParam = Class FilledTypeParam?
	NumberedRelation = "relation" Numbering?
	DefaultValidType
		= "value"
		| "statement"
		| "operation"
		| "property"
		| "bool"
		| "int"
		| "Error"
	CollectionType
		= FullArrayType
		| CopiedArrayType
		| ListType
	FullArrayType = "[" ListOf<ValidType, ","> "]"
	CopiedArrayType = "[" ValidType ";" number "]"
	ListType = "[" ValidType "]"
	
	ModBody = "{" ActionLine* "}"
	ActionLine = Action ";"
	Action
		= ModBody
		| DataDecl
		| Assignment
		| Increment | Decrement
		| Return
		| ControlFlow
		| "break"
		| "continue"
		| MethodCall
		| ModCall
	MethodCall = Variable "." Method Call
	ModCall = Mod Call
	Call = "(" ListOf<Readable, ","> ")"
	
	Assignment = Variable "=" Readable
	Readable = Literal | ControlFlow | Array | Variable
	Increment = Variable "++"
	Decrement = Variable "--"
	
	Return = ReturnOrYield ReturnUp? Readable?
	ReturnOrYield = "return" | "yield"
	ReturnUp = DotNumber | DotDot
	DotNumber = "." number
	DotDot = ".."
	
	EvalBool
		= And
		| Or
		| Not
		| Bool
		| LessThan | GreaterThan
		| EqualTo | Unequal
		| Variable						// will need to add statement.truth
	And = EvalBool1 "and" EvalBool2
	Or = EvalBool1 "or" EvalBool2
	Not = "!" EvalBool
	EvalBool1 = EvalBool
	EvalBool2 = EvalBool
		
	LessThan = Comparable "<" Comparable
	GreaterThan = Comparable ">" Comparable
	EqualTo = Equatable "==" Equatable
	Unequal = Equatable "!=" Equatable
	
	ControlFlow = IfFlow | MatchFlow | WhileFlow | ForFlow
	IfFlow = "if" EvalBool "then" Action Else?
	Else = "else" Action
	ForFlow = "for" Arg "in" Collection "do" Action
	Arg = id2
	Collection = Array | Variable
	WhileFlow = "while" EvalBool "do" Action
	MatchFlow = "match" Variable ":" ListOf<MatchLine, ",">
	MatchLine = MatchStart "=>" Action
	MatchStart
		= ValidType
		| "none"
		| IfEvalBool
	IfEvalBool = "if" EvalBool
	
	Comparable
		= Int
		| Variable					// can be a variable that's not int
	Equatable
		= Comparable
		| EvalBool
		| "none"
		| Variable
	
	Array						// range will be built-in. Count(int), Range(int, int).
		= FullArray
		| CopiedArray
	FullArray = "[" ListOf<Readable, ","> "]"
	CopiedArray = "[" Readable ";" Int "]"
	
	Variable
		= MethodCall
		| VarField
		| ModCall
		| AssignedVariable
	VarField = Variable "." Field				// unnamed field, or list index
	
	Literal
		= Bool
		| Int
		| String
	String = "\"" char* "\""
	char = ~"\"" any
	Bool = "true" | "false"
	Int
		= number
		| Variable
	
	number = digit+
	id1 = ~keyword idchar+					// for data in the header
	id2 = ~keyword idletter idchar*				// for types and variables in body
	idchar = alnum | "_"
	idletter = letter | "_"
	dot = "."
	keyword
		= "true " | "false "
		| "mut "
		| "none "
		| "value "
		| "statement "
		| "operation "
		| "property "
		| "bool "
		| "int "
		| "Error "
		| "relation "
		| "infix "
		| "struct "
		| "enum "
		| "method "
		| "mod "
		| "if "
		| "then "
		| "else "
		| "for "
		| "in "
		| "do "
		| "or "
		| "and "
		| "not "
		| "break "
		| "continue "
		| "len "
		| "class "
		| "impl "
		
	DeclaredStatement = id1
	Relation = id1
	Operation = id2
	UserDefinedInfix = id2
	Property = id1
	Value = id1
	Struct = id2
	Class = id2
	TypeParamArg = id2
	Method = id2
	Mod = id2
	Enum = id2
	AssignedVariable = id2
	Field = id2 | number
}
